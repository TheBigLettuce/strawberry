/*
  Strawberry, a music player
  Copyright (C) 2024  Bob

  This program is free software: you can redistribute it and/or modify
  it under the terms of the GNU General Public License as published by
  the Free Software Foundation, either version 3 of the License, or
  (at your option) any later version.

  This program is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
  GNU General Public License for more details.

  You should have received a copy of the GNU General Public License
  along with this program.  If not, see <https://www.gnu.org/licenses/>.
*/

plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

def keystoreProperties = {
    def prop = new Properties()
    def keystorePropertiesFile = rootProject.file('key.properties')
    if (keystorePropertiesFile.exists()) {
        prop.load(new FileInputStream(keystorePropertiesFile))
    }
    return prop
}()

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    namespace = "com.github.thebiglettuce.strawberry"
    compileSdk = 35
    ndkVersion = flutter.ndkVersion

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    kotlin { jvmToolchain(17) }

    defaultConfig {
        multiDexEnabled true

        applicationId = "com.github.thebiglettuce.strawberry"
        minSdk = 31
        targetSdk = 35
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true

            signingConfig = signingConfigs.release

            proguardFiles(
                    // Includes the default ProGuard rules files that are packaged with
                    // the Android Gradle plugin. To learn more, go to the section about
                    // R8 configuration files.
                    getDefaultProguardFile("proguard-android-optimize.txt"),
            )
        }
        debug {
            debuggable true
            applicationIdSuffix '.debug'
        }
        profile {
            applicationIdSuffix '.profile'
        }
    }
}

flutter {
    source = "../.."
}

dependencies {
    implementation "io.coil-kt.coil3:coil:3.0.4"
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.window:window:1.3.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.7'
    implementation 'androidx.window:window-java:1.3.0'
    implementation "androidx.activity:activity-ktx:1.9.3"
    implementation "com.squareup.okio:okio:3.9.1"
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation "androidx.media3:media3-exoplayer:1.4.1"
    implementation "androidx.media3:media3-common:1.4.1"
    implementation "androidx.media3:media3-session:1.4.1"
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs_nio:2.1.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:2.0.21"
}